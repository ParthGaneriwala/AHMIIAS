##!
# @file
#
# @created rjones 20210121

echo "\nLoading warn.soar"

####################
### Implementaton of the warn operator
### Issue the warning and set a flag to await the response from the user

sp {warn*apply*message
    (state <s> ^operator <o>)
    (<o> ^name warn
         ^error-info.error <e>)
-->
    (write (crlf) |Choosing to issue a warning for | <e> | error|)
    }

### Clear out any information from any previous warning we have given
sp {warn*apply*warning-accepted*remove
    :o-support
    (state <s> ^operator <o>
              -^warning-issued)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei> ^warning-accepted <x>)
-->
    (<ei> ^warning-accepted <x> -)
    }

### We need the :o-support flag on this operator because Soar wants ^warning-accepted to get o-support
### but ^warning-issued to get i-support, and that causes Soar to go haywire
sp {warn*apply*issue-warning
    :o-support
    (state <s> ^operator <o>
               ^io.output-link <out>
              -^warning-issued)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei>  ^error <e>
           ## We use the "old-value" here because it is the value the triggeered the warning,
           ## and we don't want things to mess up if the input value happens to change in
           ## the middle of the warning process
           ^old-value <v>
           ^value-changed true
          -^warning-accepted)
-->
    (<out> ^alert-sensor-error <e>
           ^alert-sensor-error-value <v>)
    (<s> ^warning-issued <e>)
    (<ei> ^value-changed true -)
    }

