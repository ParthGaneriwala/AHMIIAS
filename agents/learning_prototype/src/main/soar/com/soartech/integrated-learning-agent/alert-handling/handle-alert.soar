##!
# @file
#
# @created rjones 20210121

echo "\nLoading handle-alert.soar"

#############
### Create structures to keep information about each type of error
sp {top-state*elaborate*error-info
    (state <s> ^superstate nil)
-->
    (<s> ^error-info <gi> <gl> <li>)
    (<gi> ^error gps-imu)
    (<gl> ^error gps-lidar)
    (<li> ^error lidar-gps)

    }

### Copy current error values from input to the error-info structures
sp {top-state*elaborate*error-info*current-value*gps-imu
    (state <s> ^io.input-link.flightdata.gps-imu-error <v>
               ^error-info <ei>)
    (<ei> ^error gps-imu)
-->
    (<ei> ^current-value <v>)
    }

sp {top-state*elaborate*error-info*current-value*gps-lidar
    (state <s> ^io.input-link.flightdata.gps-lidar-error <v>
               ^error-info <ei>)
    (<ei> ^error gps-lidar)
-->
    (<ei> ^current-value <v>)
    }

sp {top-state*elaborate*error-info*current-value*lidar-imu
    (state <s> ^io.input-link.flightdata.lidar-imu-error <v>
               ^error-info <ei>)
    (<ei> ^error lidar-imu)
-->
    (<ei> ^current-value <v>)
    }

#####################
### Parameter values for the various warning conditions
### Possible warn-condition values are normal, low, high, and safety
### We never alert in a normal condition, always alert in a safety condition
### and learn whether to alert in low or high conditions

sp {top-state*elaborate*safety-error
    (state <s> ^superstate nil)
-->
    (<s> ^safety-error 10.0)
    }

sp {top-state*elaborate*high-low-warn-error
    (state <s> ^superstate nil)
-->
    (<s> ^high-low-warn-error 9.0)
    }

sp {top-state*elaborate*normal-error
    (state <s> ^superstate nil)
-->
    (<s> ^normal-error 8.0)
    }

##########################
### Detect safety condition for a sensor
sp {top-state*elaborate*error-info*warn-condition*safety
    (state <s> ^flight-mode horizontal
               ^safety-error <v>
               ^error-info <ei>)
    (<ei> ^error <e>
          ^current-value > <v>)
-->
    (<ei> ^warn-condition safety)
    }

##########################
### Detect high warn-condition for a sensor
sp {top-state*elaborate*error-info*warn-condition*high
    (state <s> ^flight-mode horizontal
               ^high-low-warn-error <v>
               ^error-info <ei>)
    (<ei> ^error <e>
          ^current-value > <v>
         -^warn-condition safety)
-->
    (<ei> ^warn-condition high)
    }

##########################
### Detect low warn-condition for a sensor
sp {top-state*elaborate*error-info*warn-condition*low
    (state <s> ^flight-mode horizontal
               ^normal-error <v>
               ^error-info <ei>)
    (<ei> ^error <e>
          ^current-value > <v>
         -^warn-condition safety
         -^warn-condition high)
-->
    (<ei> ^warn-condition low)
    }

######################
### Detect normal condition if none of the other conditions apply

sp {top-state*elaborate*error-info*warn-condition*normal
    (state <s> ^flight-mode horizontal
               ^error-info <ei>)
    (<ei> ^error <e>
         -^warn-condition safety
         -^warn-condition high
         -^warn-condition low)
-->
    (<ei> ^warn-condition normal)
    }

########################
### Any time one of the error values changes, record the change and set the ^error-changed flag
### This flag will tell us that we have to decide whether to warn the user

sp {record-changed-error*propose
    (state <s> ^error-info <ei>)
    (<ei> ^current-value <v>
         -^old-value <v>
         -^value-changed true)
-->
    (<s> ^operator <o> + =)
    (<o> ^name record-changed-error
         ^error-info <ei>
         ^value <v>)
    }

#######################
### We do not want to propose warn and do-not-warn for all three error types at the same
### time, because then some of the feedback reward for learning will get allocated to
### operators that the user was not really giving feedback on.
###
### That is, we propose warn and do-not-warn for all three error types, but we only
### "warn" for one at a time.  Based on that warning, we copute a reward signal based
### and whether the user accepts or rejects the warning.  But that reward ideally would
### only be allocated to the warn/do-not-warn operators for THAT error signal.  However,
### because we also proposed warn/do-not-warn for other operators, they will get some of
### the reward signal, as well.
###
### So instead we are going to try looking at just one error-type at a time.  A new
### operator will set the "attention-error-type" attribute to one error-type at a time
### (any error-type that is flagged with ^value-changed true).  Then we will only propose
### warn/do-not-warn for the current ^attention-error-type value.  We will not allow
### the selection of a new ^attention-error-type value until the warn/do-not-warn process
### is complete for the "currently selected" ^attention-error-type.

sp {set-attention-error-type*propose
    (state <s> ^error-info <ei>
              -^attention-error-type)
    (<ei> ^value-changed true
            ^warn-condition
            ^error <et>)
-->
    (<s> ^operator <o> +,=)
    (<o> ^name set-attention-error-type
         ^error-type <et>)
    }

### If the current attention-error-type is not marked with ^value-changed true, then
### it means we are done handling that error, so we remove the attention-error-type value.
### This will allow a new attention-error-type value to be selected, if there is one
### pending. We want this to happen immediately, so we use :o-support instead of
### proposing and applying an operator, because that could lead to race conditions.

sp {remove-attention-error-type*apply
    :o-support
    (state <s> ^error-info <ei>
               ^attention-error-type <et>)
    (<ei> ^error <et>
         -^value-changed true)
-->
    (write (crlf) |Removing attention-error-type | <et> | because it has been processed|)
    (<s> ^attention-error-type <et> -)
    }

############################
### If the ^error-changed flag is set, then we need to (re-)decide whether to issue a warning.
### when we issue the warning, we also remove the ^error-changed  flag so we do not issue
### another warning until the error value changes again

### To start, always propose the warn and do-not-warn operators
### The warn operator will issue a warning and then we will wait for the user to 
### accept (or not) the warning
### and then we will remove the ^error-changed  flag
###
### The do-not-warn operator will simply remove the flag

### The ^warning-issued value allows us to wait for the user response, and it will also prevent
### us from issuing more than one warning at a time

### Changed the conditions of these operator proposals, so they only propose for
### the current attention-error-type.  See the comments above for the explanation.
sp {warn*propose
    (state <s> ^error-info <ei>
               ^attention-error-type <et>
              -^warning-issued)
    (<ei> ^value-changed true
             ^warn-condition
             ^error <et>)
-->
    (<s> ^operator <o> +,=)
    (<o> ^name warn
         ^error-info <ei>)
    }

sp {do-not-warn*propose
    (state <s> ^error-info <ei>
               ^attention-error-type <et>)
    (<ei> ^value-changed true
            ^warn-condition
            ^error <et>)
-->
    (<s> ^operator <o> +,=)
    (<o> ^name do-not-warn
         ^error-info <ei>)
    }

####################
### Record user's response to a warning
sp {propose*record-user-resopnse
    (state <s> ^flight-mode horizontal
               ^warning-issued <e>
               ^error-info <ei>
               ^io.input-link.sensor-alert-accepted <x>)
    (<ei> ^error <e>
         -^warning-accepted)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name record-user-response
         ^error-info <ei>
         ^user-response <x>)
}


