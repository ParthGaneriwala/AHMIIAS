##!
# @file
#
# @created rjones 20210118

echo "\nLoading top-level.soar"
# sp{ (state }



# Initialize agent
sp {propose*initialize
    (state <s> ^superstate nil
               ^io.input-link.flightdata <fd>
              -^name)
    (<fd> ^throttle)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name initialize)
    (write (crlf) |Agent initializing...|)

 }

# takeoff agent
sp {propose*takeoff
    (state <s> ^name takeoff
               ^flight-mode vertical
               ^io.input-link.flightdata <fd>)
    (<fd> ^throttle <th> < 0.9)

-->
    (write (crlf) |Throttle | <th>)
    (<s> ^operator <o> + >,=)
    (<o> ^name takeoff)

}

# transition to horizontal
sp {propose*transition
    (state <s> ^name takeoff
               ^flight-mode vertical
               ^io.input-link.flightdata <fd>)
    (<fd> ^throttle >= 0.9
          ^altitude > 3200
          ^airspeed >= 0)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name transition)
    (write (crlf) |Proposing Transitioning to Horizontal mode...|)

}

# landing sequence
sp {propose*start-landing
    (state <s> ^flight-mode horizontal)
    (<s> ^io.input-link.flightdata.initiate-landing yes)
    (<s> ^landing no)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name start-landing)
}

sp {propose*landing-phase-two
    (state <s> ^flight-mode horizontal
               ^landing phase-one
               ^io.input-link.flightdata.altitude <= 4000)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name landing-phase-two)
    (write (crlf) |Proposing Landing Phase 2|)
}

sp {propose*landing-phase-three
    (state <s> ^flight-mode horizontal
               ^landing phase-two
               ^io.input-link.flightdata <fd>)
    (<fd> ^altitude <= 4050
          ^airspeed <= 180)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name landing-phase-three)
}

sp {propose*landing-phase-four
    (state <s> ^flight-mode horizontal
               ^landing phase-three
               ^io.input-link.flightdata <fd>)
    (<fd> ^altitude < 3700
          ^distance-to-target < 1.0)
-->
    (<s> ^operator <o> + =)
    (<o> ^name landing-phase-four)
}

sp {propose*landing-finished
    (state <s> ^flight-mode horizontal
               ^landing phase-four
               ^io.input-link.flightdata.airspeed <= 10)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name landing-finished)
}

# Abort Landing
sp {propose*abort-landing
    (state <s> ^io.input-link.flightdata.abort-landing yes)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name abort-landing)
}

sp {propose*transition-after-abort
    (state <s> ^landing abort
               ^io.input-link.flightdata <fd>
               ^flight-mode vertical)
    (<fd> ^altitude > 4200
          ^pilot-selected-landing-zone yes)
-->
    (<s> ^operator <o> + >,=)
    (<o> ^name transition-after-abort)
}

