##!
# @file
#
# @created rjones 20210122

echo "\nLoading selection.soar"

##############################
### Selection logic for the warn and do-not-warn operators

### Always choose to warn in the safety condition
sp {warn*best*safety-condition
    (state <s> ^operator <o> +)
    (<o> ^name warn
         ^error-info.warn-condition safety)
-->
    (<s> ^operator <o> >,=)
    }

### Always choose NOT to warn in the normal condition
sp {do-not-warn*best*normal-condition
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn
         ^error-info.warn-condition normal)
-->
    (<s> ^operator <o> >,=)
    }

################################
### Learning conditions

### if the warn-condition is low or high, then we want to
### learn the user's preferred thresholds for when to warn.
### For this simple case, we will have one rule for each warn condition times one
### rule for each error type, which allows us to learn different thresholds for each
### error type, if we want.
### If we wanted to just learn one threshold across all three error types, we would just
### use two rules.
### If we wanted to distinguish between even more cases (such as different thresholds during
### different flight modes), we would add even more rules.

sp {warn*select*low*gps
    (state <s> ^operator <o> +)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei> ^error gps
          ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*low*lidar
    (state <s> ^operator <o> +)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei> ^error lidar
          ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*low*imu
    (state <s> ^operator <o> +)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei> ^error imu
          ^warn-condition low)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*high*gps
    (state <s> ^operator <o> +)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei> ^error gps
          ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*high*lidar
    (state <s> ^operator <o> +)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei> ^error lidar
          ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

sp {warn*select*high*imu
    (state <s> ^operator <o> +)
    (<o> ^name warn
         ^error-info <ei>)
    (<ei> ^error imu
          ^warn-condition high)
-->
    (<s> ^operator <o> = 0.0)
    }

### Then we have 6 similar rules for the do-not-warn operator
### Although these are technically not necessary the way we are currently doing things.
### We have no way to get any user feedback when we choose NOT to warn, so these rules
### will never get any reward signal.  However, if we ever get to a point where we could
### give them a reward signal, it would help speed up the learning

sp {do-not-warn*any
    (state <s> ^operator <o> +)
    (<o> ^name do-not-warn
         ^error-info <ei>)
    (<ei> ^error <err>
          ^warn-condition <> safety)
-->
    (<s> ^operator <o> = 0.0)
    }



