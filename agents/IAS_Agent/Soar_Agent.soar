waitsnc --on
# Initialize agent
sp {propose*initialize
    (state <s> ^superstate nil)
    (<s> ^io.input-link.flightdata <fd>)
    (<s> -^name)
    (<fd> ^throttle)

-->
    (<s> ^operator <o> +)
    (<o> ^name initialize)
    (write (crlf) |Agent initializing...|)

}
sp {apply*initialize
    (state <s> ^operator <o>)
    (<o> ^name initialize)
    (<s> ^io.output-link <out>)
-->
    (<s> ^name takeoff)
    (<s> ^flight-mode vertical)
    (<s> ^sensor-unreliable no)
    (<s> ^faulty-sensor none)
    (<s> ^landing no)
    (<s> ^notified-pilot no)
    (<out> ^target-altitude 5000)
    (<out> ^throttle 0)
    (<out> ^autoflaps off)
    (<out> ^air-brake 0.0)
    (write (crlf) |Agent initialized.|)
}

# takeoff agent
sp {propose*takeoff
    (state <s> ^name takeoff)
    (<s> ^flight-mode vertical)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^throttle <th> < 0.9)

-->
    (write (crlf) |Throttle | <th>)
    (<s> ^operator <o> +)
    (<o> ^name takeoff)

}

sp {apply*takeoff
    (state <s> ^operator <o>)
    (<o> ^name takeoff)
    (<s> ^io.input-link.flightdata.throttle <th>)
    (<s> ^io.output-link <out>)
    (<s> ^io.output-link.throttle <tho>)

-->

    (write (crlf) |Increasing Throttle...|)
    (<out> ^throttle <tho> -)
    (<out> ^throttle (+ <th> 0.1))
}

# transition to horizontal
sp {propose*transition
    (state <s> ^name takeoff)
    (<s> ^flight-mode vertical)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^throttle <th> >= 0.9)
    (<fd> ^altitude <alt> > 3200)
    (<fd> ^airspeed <as> >= 0)
-->
    (<s> ^operator <o> +)
    (<o> ^name transition)
    (write (crlf) |Proposing Transitioning to Horizontal mode...|)

}

sp {apply*transition
    (state <s> ^operator <o>)
    (<o> ^name transition)
    (<s> ^name <n> takeoff)
    (<s> ^flight-mode <fm>)
    (<s> ^io.output-link <out>)

-->
    (<s> ^name <n> -)
    (<s> ^name flying)
    (<out> ^VTOLMode horizontal)
    (<s> ^flight-mode <fm> -)
    (<s> ^flight-mode horizontal)
    (write (crlf) |Transitioning to Horizontal mode...|)
}

sp {propose*detect*gps*error
    (state <s> ^flight-mode horizontal)
    (<s> ^sensor-unreliable no)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^gps-lidar-error <e1> > 10.0)
    (<fd> ^gps-imu-error <e2> > 10.0)
-->
    (write (crlf) |GPS is Unreliable!|)
    (write (crlf) |GPS - LIDAR error:| <e1>)
    (write (crlf) |GPS - IMU error:| <e2>)
    (<s> ^operator <o> + >)
    (<o> ^name gps-sensor-error-over-limit)
}

sp {propose*detect*lidar*error
    (state <s> ^flight-mode horizontal)
    (<s> ^sensor-unreliable no)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^gps-lidar-error <e1> > 10.0)
    (<fd> ^lidar-imu-error <e2> > 10.0)
-->
    (write (crlf) |LIDAR is Unreliable!|)
    (write (crlf) |LIDAR - GPS error:| <e1>)
    (write (crlf) |LIDAR - IMU error:| <e2>)
    (<s> ^operator <o> + >)
    (<o> ^name lidar-sensor-error-over-limit)
}

sp {propose*detect*imu*error
    (state <s> ^flight-mode horizontal)
    (<s> ^sensor-unreliable no)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^gps-imu-error <e1> > 10.0)
    (<fd> ^lidar-imu-error <e2> > 10.0)
-->
    (write (crlf) |IMU is Unreliable!|)
    (write (crlf) |IMU - GPS error:| <e1>)
    (write (crlf) |IMU - LIDAR error:| <e2>)
    (<s> ^operator <o> + >)
    (<o> ^name imu-sensor-error-over-limit)
}

# detect error in GPS value
sp {apply*detect*gps*error
    (state <s> ^sensor-unreliable <sur>)
    (<s> ^operator <o>)
    (<o> ^name gps-sensor-error-over-limit)
    (<s> ^io.output-link <out>)
    (<s> ^faulty-sensor <fs>)
    -->
    (write (crlf) |test|)
    (<s> ^sensor-unreliable <sur> -)
    (<s> ^sensor-unreliable yes)
    (<s> ^faulty-sensor <fs> -)
    (<s> ^faulty-sensor gps)
    (<out> ^GPSUnreliable yes)
}

# detect error in LIDAR value
sp {apply*detect*lidar*error
    (state <s> ^sensor-unreliable <sur>)
    (<s> ^operator <o>)
    (<o> ^name lidar-sensor-error-over-limit)
    (<s> ^io.output-link <out>)
    (<s> ^faulty-sensor <fs>)

    -->
    (<s> ^sensor-unreliable <sur> -)
    (<s> ^sensor-unreliable yes)
    (<s> ^faulty-sensor <fs> -)
    (<s> ^faulty-sensor lidar)
    (<out> ^LIDARUnreliable yes)
}

# detect error in IMU value
sp {apply*detect*imu*error
    (state <s> ^sensor-unreliable <sur>)
    (<s> ^operator <o>)
    (<o> ^name imu-sensor-error-over-limit)
    (<s> ^io.output-link <out>)
    (<s> ^faulty-sensor <fs>)

    -->
    (<s> ^sensor-unreliable <sur> -)
    (<s> ^sensor-unreliable yes)
    (<s> ^faulty-sensor <fs> -)
    (<s> ^faulty-sensor imu)
    (<out> ^IMUUnreliable yes)
}

# pilot acknowledgement
sp {propose*pilot-agrees
    (state <s> ^flight-mode horizontal)
    (<s> ^sensor-unreliable yes)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^pilot_decision agree)
-->
    (write (crlf) |Pilot agrees.|)

    (<s> ^operator <o> + >)
    (<o> ^name pilot-agrees)
}

sp {apply*pilot-agrees*gps-error
    (state <s> ^sensor-unreliable <sur>)
    (<s> ^operator <o>)
    (<o> ^name pilot-agrees)
    (<s> ^faulty-sensor gps)

    (<s> ^io.output-link <out>)

    -->
    (<s> ^sensor-unreliable <sur> -)
    (<s> ^sensor-unreliable yes)
    (<out> ^GPSUnreliable yes)
    (<out> ^sensor-to-use lidar)
}
sp {apply*pilot-agrees*lidar-error
    (state <s> ^sensor-unreliable <sur>)
    (<s> ^operator <o>)
    (<o> ^name pilot-agrees)
    (<s> ^faulty-sensor lidar)

    (<s> ^io.output-link <out>)

    -->
    (<s> ^sensor-unreliable <sur> -)
    (<s> ^sensor-unreliable yes)
    (<out> ^LIDARUnreliable yes)
    (<out> ^sensor-to-use gps)
}
sp {apply*pilot-agrees*imu-error
    (state <s> ^sensor-unreliable <sur>)
    (<s> ^operator <o>)
    (<o> ^name pilot-agrees)
    (<s> ^faulty-sensor imu)

    (<s> ^io.output-link <out>)

    -->
    (<s> ^sensor-unreliable <sur> -)
    (<s> ^sensor-unreliable yes)
    (<out> ^IMUUnreliable yes)
    (<out> ^sensor-to-use gps)
}

# pilot disagrees
sp {propose*pilot-disagrees
    (state <s> ^flight-mode horizontal)
    (<s> ^sensor-unreliable yes)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^pilot_decision disagree)
-->
    (write (crlf) |Pilot agrees.|)

    (<s> ^operator <o> + >)
    (<o> ^name pilot-disagrees)
}


# landing sequence
sp {propose*start-landing
    (state <s> ^flight-mode horizontal)
    (<s> ^io.input-link.flightdata.initiate-landing yes)
    (<s> ^landing no)
-->
    (<s> ^operator <o> + )
    (<o> ^name start-landing)
}

sp {apply*start-landing
    (state <s> ^operator <o>)
    (<s> ^landing <l> no)
    (<o> ^name start-landing)
    (<s> ^io.output-link <out>)
    (<out> ^target-altitude <ta>)
    (<out> ^throttle <tho>)
    (<out> ^air-brake <abo>)

-->
    (<out> ^throttle <tho> -)
    (<out> ^throttle 0.0)
    (<out> ^target-altitude <ta> -)
    (<out> ^target-altitude 4000)
    (<out> ^air-brake <abo> -)
    (<out> ^air-brake 0.8)
    (<s> ^landing <l> -)
    (<s> ^landing phase-one)
    (write (crlf) |Initiating Landing Phase 1|)
}

sp {propose*landing-phase-two
    (state <s> ^flight-mode horizontal)
    (<s> ^landing phase-one)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^altitude <= 4000)
-->
    (<s> ^operator <o> + )
    (<o> ^name landing-phase-two)
    (write (crlf) |Proposing Landing Phase 2|)
}

sp {apply*landing-phase-two
    (state <s> ^operator <o>)
    (<o> ^name landing-phase-two)
    (<s> ^io.input-link.flightdata <fd>)
    (<s> ^io.output-link <out>)
    (<s> ^landing <l>)
    (<out> ^throttle <tho>)
    (<out> ^autoflaps <afo>)
    (<out> ^air-brake <abo>)
-->
    (<out> ^throttle <tho> -)
    (<out> ^autoflaps <afo> -)
    (<out> ^air-brake <abo> -)
    (<out> ^throttle 0.0)
    (<out> ^autoflaps on)
    (<out> ^air-brake 1.0)
    (<s> ^landing <l> -)
    (<s> ^landing phase-two)
    (write (crlf) |Initiating Landing Phase 2|)
}

sp {propose*landing-phase-three
    (state <s> ^flight-mode horizontal)
    (<s> ^landing phase-two)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^altitude <= 4050)
    (<fd> ^airspeed <= 180)
-->
    (<s> ^operator <o> + )
    (<o> ^name landing-phase-three)
}

sp {apply*landing-phase-three
    (state <s> ^operator <o>)
    (<o> ^name landing-phase-three)
    (<s> ^io.input-link.flightdata <fd>)
    (<s> ^io.output-link <out>)
    (<s> ^landing <l>)
    (<out> ^VTOLMode <vmo>)
    (<out> ^throttle <tho>)
    (<out> ^target-altitude <ta>)
-->
    (<out> ^VTOLMode <vmo> -)
    (<out> ^VTOLMode vertical)
    (<out> ^throttle <tho> -)
    (<out> ^throttle 0.31)
    (<out> ^target-altitude <ta> -)
    (<out> ^target-altitude 3700)
    (<s> ^landing <l> -)
    (<s> ^landing phase-three)
    (write (crlf) |Initiating Landing Phase 3|)
}
sp {propose*landing-phase-four
    (state <s> ^flight-mode horizontal)
    (<s> ^landing phase-three)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^altitude < 3700)
    (<fd> ^distance-to-target < 1.0)
-->
    (<s> ^operator <o> + )
    (<o> ^name landing-phase-four)
}

sp {apply*landing-phase-four
    (state <s> ^operator <o>)
    (<o> ^name landing-phase-four)
    (<s> ^io.output-link <out>)
    (<s> ^landing <l>)
    (<out> ^throttle <tho>)
    (<out> ^target-altitude <ta>)
-->
    (<out> ^throttle <tho> -)
    (<out> ^throttle 0.195)
    (<out> ^target-speed 0)
    (<out> ^target-altitude <ta> -)
    (<out> ^target-altitude 2250)
    (<s> ^landing <l> -)
    (<s> ^landing phase-four)
    (write (crlf) |Initiating Landing Phase 4|)
}
sp {propose*landing-finished
    (state <s> ^flight-mode horizontal)
    (<s> ^landing phase-four)
    (<s> ^io.input-link.flightdata <fd>)
    (<fd> ^airspeed <= 10)
-->
    (<s> ^operator <o> + )
    (<o> ^name landing-finished)
}

sp {apply*landing-finished
    (state <s> ^operator <o>)
    (<o> ^name landing-finished)
    (<s> ^io.output-link <out>)
    (<s> ^landing <l>)
    (<out> ^throttle <tho>)
-->
    (<out> ^throttle <tho> -)
    (<out> ^throttle 0.23)
    #(<out> ^rotor-pos 0.99)
    (<s> ^landing <l> -)
    (<s> ^landing landed)
    (write (crlf) |Finished Landing|)
}

# Abort Landing
sp {propose*abort-landing
    (state <s> ^io.input-link.flightdata <fd>)
    (<fd> ^abort-landing yes)
-->
    (<s> ^operator <o> + >)
    (<o> ^name abort-landing)
}
sp {apply*abort-landing
    (state <s> ^operator <o>)
    (<s> ^io.input-link.flightdata <fd>)
    (<o> ^name abort-landing)
    (<s> ^landing <l>)
    (<s> ^io.output-link <out>)
    (<out> ^VTOLMode <vtol>)
    (<out> ^throttle <tho>)
    (<out> ^autoflaps <afo>)
    (<out> ^air-brake <abo>)
    (<s> ^flight-mode <fm>)
    #(<out> ^target-speed <ts>)
    (<fd> ^abort-landing <ab>)
-->
    (<s> ^landing <l> -)
    (<s> ^landing abort)
    #(<out> ^target-speed <ts> -)
    (<out> ^VTOLMode <vtol> -)
    (<out> ^VTOLMode vertical)
    (<out> ^throttle <tho> -)
    (<out> ^autoflaps <afo> -)
    (<out> ^air-brake <abo> -)
    (<out> ^throttle 0.9)
    (<out> ^autoflaps off)
    (<out> ^air-brake 0.0)
    (<s> ^flight-mode <fm> -)
    (<s> ^flight-mode vertical)
    (<fd> ^abort-landing <ab> -)
    (<fd> ^abort-landing no)
    (write (crlf) |Aborting Landing|)
}

sp {propose*transition*after*abort
    (state <s> ^landing abort)
    (<s> ^io.input-link.flightdata <fd>)
    (<s> ^flight-mode vertical)
    (<fd> ^altitude <alt> > 4200)
    (<fd> ^pilot-selected-landing-zone yes)
-->
    (<s> ^operator <o> + >)
    (<o> ^name transition-after-abort)
}
sp {apply*transition*after*abort
    (state <s> ^operator <o>)

    (<o> ^name transition-after-abort)
    (<s> ^flight-mode <fm>)
    (<s> ^io.input-link.flightdata <fd>)
    (<s> ^io.output-link <out>)
    (<out> ^VTOLMode <vm>)
    (<out> ^target-altitude <tao>)
    (<s> ^landing <l>)
-->
    (<out> ^VTOLMode <vm> -)
    (<out> ^VTOLMode horizontal)
    (<out> ^target-altitude <tao> -)
    (<out> ^target-altitude 5000)
    (<s> ^flight-mode <fm> -)
    (<s> ^flight-mode horizontal)
    (<s> ^landing <l> -)
    (<s> ^landing no)
    (write (crlf) |Transitioning to Horizontal mode after abort...|)
}
